apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.dcendents.android-maven'

apply from: "$rootDir/tests.gradle"

android {
    compileSdkVersion targetSdk
    buildToolsVersion buildTools

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion targetSdk
        versionCode libVersionCode
        versionName libVersionName
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
}

dependencies {
    api 'com.bluelinelabs:conductor:2.1.4'
    implementation "com.android.support:appcompat-v7:$supLibVer"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation "org.jetbrains.anko:anko-coroutines:$anko"
}

task verifyVersion << {
    println()
    println "VERSION CODE $android.defaultConfig.versionCode"
    println()
    println "VERSION NAME $android.defaultConfig.versionName"
    println()
}

ext {
    bintrayRepo = 'maven'
    bintrayName = 'konductor-arch'

    publishedGroupId = 'com.arnis'
    libraryName = 'konductor-arch'
    artifact = 'konductor-arch'

    libraryDescription = ''

    siteUrl = 'https://github.com/arnis71/arch'
    gitUrl = 'https://github.com/arnis71/arch.git'

    libraryVersion = libVersionName

    developerId = 'arnis71'
    developerName = 'Arnis'
    developerEmail = 'arnis71@gmail.com'
    organization = 'arnis71'
    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

apply from: 'https://raw.githubusercontent.com/quangctkm9207/template-files/master/android/gradle/bintray.gradle'

group = publishedGroupId // Maven Group ID for the artifact

install {
    repositories.mavenInstaller {
        // Generate POM.xml with proper parameters
        pom.project {
            packaging 'aar'
            groupId publishedGroupId
            artifactId artifact

            // Add your description here
            name libraryName
            description libraryDescription
            url siteUrl

            // Set your license
            licenses {
                license {
                    name licenseName
                    url licenseUrl
                }
            }
            developers {
                developer {
                    id developerId
                    name developerName
                    email developerEmail
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl
            }
        }

    }
}